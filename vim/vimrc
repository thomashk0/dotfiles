" vim: set ft=vim sw=4 ts=4 sts=4 tw=80 foldmarker={,} foldlevel=0 foldmethod=marker:
" ============================================================================ "
" A minimal Vim configuration
"
" Author: Thomas Hiscock <thomashk000@gmail.com>
" ============================================================================ "

" Configuration variables {

" Force this option to 0 to disable bepo keyboard bindings
let g:use_bepo_keyboard = 1

" Enable some mandatory plugins
let g:use_plugins = 1

" Enable nicer colorschemes (requires g:use_plugins = 1)
let g:use_extra_colorschemes = 1

" Enable some extra (non-essential) plugins
let g:use_plugins_extra = 0

" }
" General options (Plugins, UI, etc.) {

set nocp
if (g:use_plugins == 1)
    call plug#begin('~/.vim/plugged')
        Plug 'tpope/vim-surround'
        Plug 'tpope/vim-commentary'
        Plug 'godlygeek/tabular'
        Plug 'sheerun/vim-polyglot'
        Plug 'junegunn/fzf', { 'do': { -> fzf#install() } }
        Plug 'junegunn/fzf.vim'

        " Usually I don't use those.
        if (g:use_plugins_extra)
            Plug 'rhysd/vim-clang-format', { 'for' : ['c', 'cpp'] }
            Plug 'SirVer/ultisnips'
            Plug 'honza/vim-snippets'
        endif

        if (g:use_extra_colorschemes == 1)
            Plug 'rafi/awesome-vim-colorschemes'
        endif
    call plug#end()
endif

filetype on
filetype indent on
filetype plugin on

" No backup and swap files
set nobackup nowb noswapfile

" Styling
set nospell

set t_Co=256
set background=dark
if (g:use_extra_colorschemes == 1)
    colorscheme apprentice
else
    colorscheme desert
endif

syntax enable
set number
set relativenumber
set list
set listchars=tab:›-,trail:.,extends:#,nbsp:.
set cc=80

" Status bar
set showcmd ruler laststatus=2
set statusline=%!MyStatusLine()

" Tab, whitespaces, indent
set expandtab
set tabstop=4
set shiftwidth=4
set softtabstop=4
set smarttab
set smartindent
set nowrap
set scrolloff=10  " Number of lines shown before the cursor

set ignorecase
set hlsearch            " Highlight search results
set noincsearch
" set incsearch           " Search while typing the search string
" by default, make vim only complete in current directory
set path=.
" To support glob-like search use:
" set path+=**
set wildmenu
" Filter-out files from file searches
set wildignore+=*.o,*.obj,*.bin,*.dll,*.exe
set wildignore+=*/.git/*,*/.svn/*,*/__pycache__/*,*/build/**
set wildignore+=*.pyc
set wildignore+=*.DS_Store
set wildignore+=*.aux,*.bbl,*.blg,*.brf,*.fls,*.fdb_latexmk,*.synctex.gz,*.pdf

let g:netrw_banner=0     " Completely useful, :help netrw-quickmap is much better
let g:netrw_liststyle=3  " Tree-like view
let g:netrw_list_hide=netrw_gitignore#Hide()
let g:netrw_list_hide.=',\(^\|\s\s\)\zs\.\S\+'


" }
" Keybindings {

" Leader is set as space
let mapleader = " "
let maplocalleader = ","

set backspace=indent,eol,start
set whichwrap+=<,>,h,l

if (g:use_bepo_keyboard == 1)
    " Remap basic movements
    noremap n l
    noremap r k
    noremap s j
    noremap t h
    noremap T H

    noremap h t
    noremap H T
    noremap j s
    noremap l n
    noremap k r

    noremap è ^
    noremap È 0

    " Fast moves between windows.
    " nnoremap <C-r> <C-W>k
    " nnoremap <C-s> <C-W>j
    " nnoremap <C-t> <C-W>h
    " nnoremap <C-n> <C-W>l

    highlight NbSp ctermbg=lightgray guibg=lightred
    match NbSp /\%xa0/

    " Completion bindings
    " See :help inc-completion
    imap <localleader>o <C-X><C-O>
    imap <localleader>l <C-X><C-L>
    imap <localleader>t <C-X><C-]>
    imap <localleader>p <C-X><C-P>
    imap <localleader>f <C-X><C-F>
    vmap <localleader>f :ClangFormat<CR>

    " C-_ maps to C-/ in reality…
    " In normal mode, also move down ;)
    nmap <C-_> gccs
    vmap <C-_> gc

    " File modification
    nmap <leader>s :w<CR>
    nmap <leader>q :q!<CR>

    nmap <leader>t <C-]>
    nmap <leader>n <C-T>

    let g:UltiSnipsExpandTrigger="<C-a>"
    let g:UltiSnipsJumpForwardTrigger="<C-b>"
    let g:UltiSnipsJumpBackwardTrigger="<C-z>"
    let g:UltiSnipsEditSplit="vertical"

endif

nmap <F2> :call ToggleSpell()<CR>
nmap <F3> :call DeleteTrailingWS()<CR>
nmap <F4> :ClangFormat<CR>
imap <F5> FIXME(ThomasH, <C-R>=strftime("%d %b %Y")<CR>):

" Fzf {
nmap <leader>ft :BTags<CR>
nmap <leader>fl :BLines<CR>
nmap <leader>fb :Buffers<CR>
nmap <leader>ff :Files<CR>
nmap <leader>fm :Marks<CR>
nmap <leader>fj :Jumps<CR>
nmap <leader>fh :History<CR>
if has('nvim')
    " temporary fix, the :GFiles commands fail with neovim :(
    nmap <leader>fg :call fzf#run({'source': 'git ls-files', 'sink': 'e'})<CR>
else
    nmap <leader>fg :GFiles<CR>
endif
" }

" Buffers {
nmap <leader>bd :bdel
nmap <leader>bn :bnext
nmap <leader>bp :bprev
nmap <leader>bl :blast
nmap <leader>bf :bfirst
nmap <leader><leader> :bmod<CR>
" }

" FIXME: recursively send 'zo' open to match fold level.
" keymap('n', '<leader>no', 'zMzA')
" Move to start of next fold
nmap <leader>nn zj
" Move to start of previous fold
nmap <leader>np zk

" Quicfix {
nmap <leader>cn :cnext<CR>
nmap <leader>co :copen<CR>
nmap <leader>cc :cclose<CR>
nmap <leader>cp :cprev<CR>
nmap <leader>cf :cfirst<CR>
nmap <leader>cl :clast<CR>
" }

"}
" Utility functions {

func! DeleteTrailingWS()
    exe "normal mz"
    %s/\s\+$//ge
    exe "normal `z"
endfunc

" Spell Check
let b:myLang = 0
let g:myLangList=["nospell","fr","en_gb"]
function! ToggleSpell()
  let b:myLang = b:myLang + 1
  if b:myLang >= len(g:myLangList)
      let b:myLang=0
  endif
  if b:myLang==0
    setlocal nospell
  else
    exe "setlocal spell spelllang=".get(g:myLangList, b:myLang)
  endif
  echo "spell checking language:" g:myLangList[b:myLang]
endfunction

function! MyStatusLine()
    let statusline = ""
    " Filename (F -> full, f -> relative)
    let statusline .= "%f"
    " Buffer flags
    let statusline .= "%(%h%1*%m%*%r%w%) "
    " File format and type
    let statusline .= "(%{&ff}%(\/%Y%))"
    " Left/right separator
    let statusline .= "%="
    " Line & column
    let statusline .= "(%l,%c%V) "
    " Character under cursor (decimal)
    let statusline .= "%03.3b "
    " Character under cursor (hexadecimal)
    let statusline .= "0x%02.2B "
    " File progress
    let statusline .= "| %P/%L"
    return statusline
endfunction

" }
" Per language tweaks {

function! SetupPython()
    setlocal errorformat=%f:%l:%c:\ %m
    setlocal makeprg=ruff\ check\ --output-format\ concise
endfunction
autocmd FileType python call SetupPython()

" VHDL {
function! SetupVHDLConfig()
    setlocal comments^=:--!
    setlocal comments^=:--
    setlocal foldmethod=marker
    " Fold Doxygen groups
    setlocal foldmarker=@{,@}
endfunction
autocmd FileType vhdl call SetupVHDLConfig()
" }

" Makefile & configuration {
autocmd FileType make setlocal ts=8 sts=8 sw=8 noexpandtab
autocmd FileType conf setlocal ts=8 sts=8 sw=8 noexpandtab
" }

" Bepo compatibility for netrw {
augroup netrw_maps
  autocmd!
  autocmd filetype netrw call ApplyNetrwMaps()
augroup END

function ApplyNetrwMaps()
  nnoremap <buffer> r k
  nnoremap <buffer> s j
  nnoremap <buffer> n <CR>
endfunction
" }

" }
